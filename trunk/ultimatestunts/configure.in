dnl Process this file with autoconf to produce a configure script.

AC_INIT(ultimatestunts/main.cpp)
AM_INIT_AUTOMAKE(ultimatestunts,0.3.1)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Vind de juiste extensies
AC_EXEEXT
AC_OBJEXT

dnl Dit bestand komt oorspronkelijk van tuxkart.


dnl ----------------------
dnl Initially no libraries
dnl ----------------------
graphlibs=""
netlibs=""
soundlibs=""
sdllibs=""

dnl ----------------------------------
dnl check for OpenGL related libraries
dnl ----------------------------------
AC_CHECK_HEADER(windows.h)

if test "x$ac_cv_header_windows_h" != "xyes" ; then

  dnl Check for X11

  AC_PATH_XTRA

  x_suffix="$X_LIBS $X_PRE_LIBS -lX11 -lXi -lXext -lXmu $X_EXTRA_LIBS -lm"
  graphlibs=$x_suffix

  dnl Reasonable stuff non-windoze variants ... :-)

  AC_CHECK_LIB(GL, glNewList, graphlibs="-lGL $graphlibs" ,,$graphlibs)
  if test "x$ac_cv_lib_GL_glNewList" = "xno" ; then
    dnl if no GL, check for MesaGL
    AC_CHECK_LIB(MesaGL, glNewList,graphlibs="-lMesaGL $graphlibs",,$graphlibs)
  fi

  dnl if using mesa, check for xmesa.h
  if test "x$ac_cv_lib_MesaGL_glNewList" = "xyes" ; then
    AC_CHECK_HEADER(GL/xmesa.h)
    AM_CONDITIONAL(ENABLE_XMESA_FX, \
                test "x$ac_cv_header_GL_xmesa_h" = "xyes")
  else
    dnl force a failed check
    AM_CONDITIONAL(ENABLE_XMESA_FX, test "no" = "yes")
  fi

  AC_CHECK_LIB(GLU, gluLookAt, graphlibs="-lGLU $graphlibs" ,,$graphlibs)
  if test "x$ac_cv_lib_GLU_gluLookAt" = "xno" ; then
    dnl if no GLU, check for MesaGLU
    AC_CHECK_LIB(MesaGLU, gluLookAt, graphlibs="-lMesaGLU $graphlibs" ,,$graphlibs)
  fi

else
  dnl CygWin under Windoze.

  graphlibs="-lm -lwinmm -lglu32 -lopengl32 -luser32 -lgdi32"
fi

dnl Check for SDL
AM_PATH_SDL(1.1.0, AC_DEFINE(HAVE_SDL))
CFLAGS="$CFLAGS $SDL_CFLAGS"
sdllibs="$SDL_LIBS $sdllibs"

dnl ---------------------
dnl Check sockets library
dnl ---------------------
AC_CHECK_LIB(socket, socket,netlibs="-lsocket $netlibs")
AC_CHECK_LIB(nsl, gethostbyname,netlibs="-lnsl $netlibs")
AC_CHECK_LIB(resolv, inet_aton,netlibs="-lresolv $netlibs")


dnl -----------------------
dnl Checks for header files
dnl -----------------------
AC_HEADER_STDC

AC_CHECK_HEADER(GL/gl.h)
dnl AC_CHECK_HEADER(windows.h, AC_DEFINE(WIN32))
dnl AC_CHECK_HEADER(joystick.h, AC_DEFINE(JOYSTICK_IS_PRESENT))
dnl AC_CHECK_HEADER(linux/joystick.h, AC_DEFINE(LINUX_JOYSTICK_IS_PRESENT))


dnl ------------------------
dnl Check fmod audio library
dnl ------------------------
AC_CHECK_LIB(fmod, FSOUND_Close,soundlibs="-lfmod $soundlibs")
AC_CHECK_HEADER(fmod/fmod.h, AC_DEFINE(FMOD_HEADER))


dnl -------------------
dnl Set the c/c++ flags
dnl -------------------
if test "x$ac_cv_header_windows_h" != "xyes" ; then
  if test "x$GXX" != "xyes" ; then
    CFLAGS="$CFLAGS -I/usr/local/include -L/usr/local/lib"
    CXXFLAGS="$CXXFLAGS -I/usr/local/include -L/usr/local/lib"
  fi
else
  CFLAGS="$CFLAGS $MACH -malign-double -I/usr/local/include -L/usr/local/lib"
  CXXFLAGS="$CXXFLAGS $MACH -malign-double -I/usr/local/include -L/usr/local/lib"
fi
CFLAGS="-Wall $CFLAGS"
CXXFLAGS="-Wall $CXXFLAGS"


dnl ------
dnl Output
dnl ------
echo "Network libraries: $netlibs"
echo "Sound libraries: $soundlibs"
echo "Graphics libraries: $graphlibs"
echo "SDL libraries: $sdllibs"

AC_SUBST(netlibs)
AC_SUBST(soundlibs)
AC_SUBST(graphlibs)
AC_SUBST(sdllibs)

AC_OUTPUT(Makefile shared/Makefile simulation/Makefile graphics/Makefile filetransfer/Makefile stuntsserver/Makefile stuntsai/Makefile stunts3dedit/Makefile trackedit/Makefile ultimatestunts/Makefile)
